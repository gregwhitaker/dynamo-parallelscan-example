import com.amazonaws.services.dynamodbv2.model.AttributeDefinition
import com.amazonaws.services.dynamodbv2.model.KeySchemaElement
import com.amazonaws.services.dynamodbv2.model.KeyType
import com.amazonaws.services.dynamodbv2.model.ScalarAttributeType
import com.nike.pdm.localstack.aws.dynamodb.CreateDynamoDbTableTask

plugins {
    id "idea"
    id "java"
    id "org.springframework.boot"           version "2.3.0.RELEASE"
    id "com.avast.gradle.docker-compose"    version "0.13.0"
    id "com.nike.pdm.localstack"            version "0.1.0"
}

project.group   = "com.github.gregwhitaker"
project.version = "0.1.0"

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation platform("org.springframework.boot:spring-boot-dependencies:2.3.0.RELEASE")

    implementation "com.amazonaws:aws-java-sdk-dynamodb:1.11.821"
    implementation "org.apache.commons:commons-lang3:3.11"
    implementation "org.springframework.boot:spring-boot-starter"
}

// Docker Compose
dockerCompose {
    useComposeFiles = [ 'localstack/localstack-docker-compose.yml' ]
}

// LocalStack
task setupLocalTable(type: CreateDynamoDbTableTask) {
    tableName = 'catalog.products'
    keySchema = [
            new KeySchemaElement("id", KeyType.HASH)
    ]
    attributeDefinitions = [
            new AttributeDefinition("id", ScalarAttributeType.S)
    ]
    initializer = 'example.buildsrc.dynamodb.ProductTableInitializer'
}